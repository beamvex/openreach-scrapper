data "aws_cloudfront_cache_policy" "caching_optimized" {
  name = "Managed-CachingOptimized"
}

resource "aws_cloudfront_origin_access_control" "openreach" {
  name                              = "openreach-scrapper-oac"
  description                       = "OAC for openreach-scrapper S3 origin"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}

resource "aws_cloudfront_distribution" "openreach" {
  enabled             = true
  default_root_object = "index.html"

  origin {
    domain_name              = aws_s3_bucket.openreach.bucket_regional_domain_name
    origin_id                = "s3-${aws_s3_bucket.openreach.bucket}"
    origin_access_control_id = aws_cloudfront_origin_access_control.openreach.id
  }

  default_cache_behavior {
    target_origin_id       = "s3-${aws_s3_bucket.openreach.bucket}"
    viewer_protocol_policy = "redirect-to-https"

    allowed_methods = ["GET", "HEAD"]
    cached_methods  = ["GET", "HEAD"]
    compress        = true

    cache_policy_id = data.aws_cloudfront_cache_policy.caching_optimized.id
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    cloudfront_default_certificate = true
  }
}